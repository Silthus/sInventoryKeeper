buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id 'kr.entree.spigradle' version '2.1.1'
    id 'java'
    id 'idea'
    id 'jacoco'
}

apply from: "$rootDir/gradle/jacoco.gradle"
apply from: "$rootDir/gradle/publish.gradle"

sourceCompatibility = 8
targetCompatibility = 8

ext {
    //Define one of the supported mc versions
    mcVersion = project.property("mcVersion")
    packageName = project.property("packageName")
}

group project.property("group")

spigot {
    pluginName = project.property("pluginName")
    authors = [project.property("author")]
    apiVersion = project.property("apiVersion")
    load = STARTUP
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

archivesBaseName = project.property("pluginName")

repositories {
    mavenCentral()
    jcenter()

    jitpack()
    spigot()

    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://oss.sonatype.org/content/repositories/central' }

    //Add your repositories here
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }

    maven { url = "https://repo.codemc.org/repository/maven-public" }
}

dependencies {
    compileOnly lombok()
    annotationProcessor lombok()

    compileOnly spigot(mcVersion)

    //Add dependencies here
    implementation 'net.silthus:slib:1.5.2'
    implementation 'net.silthus.configlib:configlib-bukkit:2.6.0'
    implementation 'co.aikar:acf-bukkit:0.5.0-SNAPSHOT'

    implementation 'org.bstats:bstats-bukkit:1.7'

    //Test dependencies
    testImplementation 'com.google.jimfs:jimfs:1.1'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'
    testImplementation "org.mockito:mockito-core:3.+"
    testImplementation 'com.github.seeseemelk:MockBukkit:v1.15-SNAPSHOT'
    testImplementation 'org.assertj:assertj-core:3.16.1'
    testImplementation lombok()
    testAnnotationProcessor lombok()
}

shadowJar {
    classifier = ''
    dependencies {
        include(dependency('co.aikar:acf-bukkit:'))
        include(dependency('org.bstats:bstats-bukkit:'))
        include(dependency('net.silthus:slib:'))
        include(dependency('net.silthus.configlib:configlib-bukkit:'))
    }
    relocate 'co.aikar.commands', "${packageName}.acf"
    relocate 'co.aikar.locales', "${packageName}.locales"
    relocate 'org.bstats', "${packageName}.bstats"
    relocate 'net.silthus.slib', "${packageName}.slib"
    relocate 'de.exlll.configlib', "${packageName}.configlib"
    relocate 'com.netflix.governator', "${packageName}.governator"
    relocate 'com.google.inject', "${packageName}.guice"
}

tasks.build.dependsOn(shadowJar)
tasks.publish.dependsOn(shadowJar)

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    ignoreFailures = true
}

processResources {
    project.properties.put("version", this.version)
    expand project.properties
}

task copyPluginYaml(type: Copy, dependsOn: generateSpigotDescription) {
    from(new File(generateSpigotDescription.temporaryDir, "plugin.yml"))
    into(sourceSets.test.resources.srcDirs[0])
}

tasks.test.dependsOn(copyPluginYaml)

defaultTasks 'build'
